## --- Markdown grammar specifications ---------------------------------------

<MD text>       ::= <MD line> <MD text'>
<MD text'>      ::= <MD line> <MD text'> | EPS

<MD line>       ::= <block element>   |  <text and span elements>
                 |  ' ' <MD line'>

<MD line'>      ::= <block element>   |  <text and span elements>
                 |  ' ' <MD line''>

<MD line''>     ::= <block element>   |  <text and span elements>
                 |  ' ' <MD line'''>

<MD line'''>    ::= <block element>   |  <text and span elements>
                 |  ' ' <code block>


## --- Elements --------------------------------------------------------------

<block element>                 ::= <header atx>                    ## first = {'#'}
                                 |  <blockquote>                    ## first = {"> "}
                                 |  <list>                          ## first = {'-', '+', '*'}
                                 |  <horizontal rule>               ## first = {'-', '*'}
                                 |  <link or reference>             ## first = {'['}
                                 |  <image>                         ## first = {'!'}
                                                                                                ## CAUTION: ambiguities there:
<text and span elements>        ::= <html entity>                   ## first = {'&'}
                                 |  <html tag or automatic ilnk>    ## first = {'<'}
                                 |  <breakline>                     ## first = {' '}                ##  2. to be grouped with 5., 6. and 11.
                                 |  <emphasis or strong style>      ## first = {'*', '_'}
                                 |  <star>                          ## first = {' '}                ##  5. to be grouped with 2., 6. and 11.
                                 |  <underscore>                    ## first = {' '}                ##  6. to be grouped with 2., 5. and 11.
                                 |  <inlined code>                  ## first = {'`'}                ##  7.
                                 |  <escape>                        ## first = {'\\'}               ##  9.
                                 |  <paragraph end>                 ## first = {'\n'}               ## 10.
                                 |  <any chars not in ' & < \n * _ ` \ # \> [ ! >                    ## 11. to be grouped with 2., 5. and 6.



<emphasis or strong style>      ::= '*' <emph or strong *>
                                 |  '_' <emph or strong _>

<emph or strong *>              ::= '*' <any chars but *> '*' '*'      ## i.e. strong
                                 |  <any chars but *> '*'               ## i.e. emphasis

<emph or strong _>              ::= '_' <any chars but *> '_' '_'      ## i.e. strong
                                 |  <any chars but *> '_'               ## i.e. emphasis

<html tag>                      ::= <any chars> <html tag'>
<html tag'>                     ::= '/' '>'
                                 |  '>'

<html tag or automatic ilnk>    ::= '<' <html tag or automatic link'>
<html tag or automatic link'>   ::= '/' <any chars> '>'
                                 |  <any chars> <html tag or automatic link">
<html tag or automatic link">   ::= <url'> '>'                                      ## first = {'://'}      ## (automatic link)
                                 |  <html tag'>                                     ## first = {'/', '>'}   ## (HTML tag)

<inlined link>              ::= '[' <url> <title> ']'

<link>                      ::= <inlined link>                                      ## first = {'['}
                             |  <referenced link>                                   ## first = {'('}

<link or reference>         ::= '[' <linked text> ']' <link or reference'>
<link or reference'>        ::= ':' <reference>                                     ## first = {':'}}
                             |  <link>                                              ## first = {'[', '('}

<linked text>               ::= <any chars but ]> 

<reference>                 ::= <spaces or not> <url> <spaces or not> <reference title> '\n'

<referenced link>           ::= <spaces or not> '[' <referenced link'> ']'          ## first = {' ', '['}
<reference linked'>         ::= <any chars but ]>
                             |  EPS

<reference title>           ::= '\n' <max 3 spaces> <reference title'>
                             |  <reference title'>
<reference title'>          ::= '"' <any chars but newline and "> '"' <spaces or not> '\n'  ## (maybe end of line not needed)

<title>                     ::= '"' <any chars but newline and "> '"'

<url>                       ::= <alphanum chars> <url'>
<url'>                      ::= "://" <any chars but ] and ">



## --- Markdown Tags ---------------------------------------------------------

<blockquotes>           ::= "> " <blockquotes'>
<blockquotes'>          ::= "> " <blockquotes'>  |  <text with span elements> <blockquotes">
<blockquotes">          ::= <text with span elements> <blockquotes">  |  '\n'  |  <ENDOFTEXT>

<breakline>             ::= "  "  <breakline'>
<breakline'>            ::= ' ' <breakline'>  |  '\n'

<code block>            ::= <spaces 4> <text with span elements> <code block'>  |  <tab> <text with span elements> <code block'>
<code block'>           ::= <spaces 4> <text with span elements> <code block'>  |  <tab> <text with span elements> <code block'>  |  '\n'  |  <ENDOFTEXT>

<escape>                ::= '\\' <escaped char>
<escaped char>          ::= '\\'  |  '`'  |  '*'  |  '_'  |  '{'  |  '}'  |  '['  |  ']'  |  '('  |  ')'  |  '#'  |  '+'  |  '-'  |  '.'  |  '!'

<header>                ::= <header atx>  |  <header setext>

<header atx>            ::= '#' <header atx'>
<header atx'>           ::= '#' <header atx'>  |  ' ' <text with span elements>

<header setext>         ::= <text with span elements> <header setext'>
<header setext'>        ::= '=' <header setext h1>
                         |  '-' <header setext h2>
                  
<header setext h1>      ::= '=' <header setext h1>  |  '\n'
<header setext h2>      ::= '-' <header setext h2>  |  '\n'

<horizontal rule>       ::= <hrule star>  |  <hrule hyphen'>

<hrule hyphen>          ::= '-' <spaces or not> '-' <spaces or not> '-' <hrule hyphen'>
<hrule hyphen'>         ::= <spaces or not> '*' <hrule hyphen'>  |  '\n'

<hrule star>            ::= '*' <spaces or not> '*' <spaces or not> '*' <hrule star'>
<hrule star'>           ::= <spaces or not> '*' <hrule star'>  |  '\n'

<html entity>           ::= '&' <non space chars> ';'

<html tag>              ::= '<' <chars> '>'

<image>                 ::= '!' <link>

<inlined code>          ::= '`' <any char but `> '`'
                         |  "``" <inlined code'>
<inlined code'>         ::= <any chars but `>  |  '`' <inlined code">
<inlined code">         ::= <any chars but `> <inlined code'>  |  '`'

<item'>                 ::= <text with span elements> <line'>  |  '\n'

<list>                  ::= <ordered list>  |  <unordered list>  ## CAUTION: "paragraphed" list items are not addressed there

<list bullet>           ::= '*' | '+' | '-'

<list'>                 ::= <text with span elements> <list'> | '\n'

<max 3 spaces>          ::= ' ' <max 3'>  |  EPS
<max 3'>                ::= ' ' <max 3''>  |  EPS
<max 3''>               ::= ' '  |  EPS

<ordered list>          ::= <ordered item> <ordered list'>
<ordered list'>         ::= <ordered item> <ordered list'> | '\n'  |  <ENDOFTEXT>

<ordered item>          ::= <number> '.' <rest of line> <item'>

<paragraph end>         ::= '\n' '\n'

<spaces or not>         ::= ' ' <spaces or not>  |  EPS                                         ### ok

<spaces4>               ::= "    "

<star>                  ::= " * "

<tab>                   ::= '\t'

<underscore>            ::= " _ "

<unordered list>        ::= <unordered item> <unordered list'>
<unordered list'>       ::= <unordered item> <unordered list'>  |  '\n'  |  <ENDOFTEXT>

<unordered item>        ::= <list bullet> <text with span elements> <item'>

