## --- Markdown grammar specifications ---------------------------------------
##
## https://tools.ietf.org/html/rfc7764 (Markdown specs)
##
## https://gitlab.com/gitlab-org/gitlab-ce/blob/master/doc/user/markdown.md#inline-diff
## https://docs.gitlab.com/ee/user/markdown.html
## https://help.github.com/articles/basic-writing-and-formatting-syntax/
##

<MD text>                       ::= <MD line> <MD text>
                                 | EPS

<MD line>                       ::= <block element>
                                 |  <space-starting elements>
                                 |  <text with span elements>


## --- Elements --------------------------------------------------------------

<block element>                 ::= <blockquote>
                                 |  <header atx>
                                 |  <header setext>
                                 |  <list>
                                 |  <horizontal rule>
                                 |  <link or reference>
                                 |  <image>
 
 
<space-starting elements>       ::= ' ' <space elements'>
                                 |  '\t' <code block>
<space elements'>               ::= ' ' <space elements''>
                                 |  '\t' <code block>
                                 |  <maybe star>
                                 |  <maybe underscore>
                                 |  <text with span elements>
<space elements''>              ::= ' ' <space elements'''>
                                 |  '\t' <code block>
                                 |  '\n'                                                ## breakline! - CAUTION: maybe context sensitive...
                                 |  <maybe star>
                                 |  <maybe underscore>
                                 |  <text with span elements>
<space elements'''>             ::= ' ' <code block>                
                                 |  '\t' <code block>
                                 |  <maybe star>
                                 |  <maybe underscore>
                                 |  <text with span elements>

                                 
<text with span elements>       ::= <html entity> <maybe setext header>
                                 |  <html tag or automatic link> <maybe setext header>
                                 |  <emphasis or strong style> <maybe setext header>
                                 |  <inlined code> <maybe setext header>
                                 |  <escape> <maybe setext header>
                                 |  <line or paragraph end>
                                 |  <any chars but & < * _ ` \\ \n> <maybe setext header>




## --- Markdown Tags ---------------------------------------------------------

<blockquotes>                   ::= "> " <blockquotes'>
<blockquotes'>                  ::= "> " <blockquotes'>  |  <text with span elements> <blockquotes">
<blockquotes">                  ::= <text with span elements> <blockquotes">
                                 |  '\n'
                                 |  <ENDOFTEXT>

<code block>                    ::= <text with span elements> <code block'>
<code block'>                   ::= <text with span elements> <code block'>
                                 |  '\n'
                                 |  <ENDOFTEXT>

<emph or strong *>              ::= '*' <any chars but *> '*' '*'                       ## i.e. strong
                                 |  <any chars but *> '*'                               ## i.e. emphasis

<emph or strong _>              ::= '_' <any chars but *> '_' '_'                       ## i.e. strong
                                 |  <any chars but *> '_'                               ## i.e. emphasis

<emphasis or strong style>      ::= '*' <emph or strong *>
                                 |  '_' <emph or strong _>

<escape>                        ::= '\\' <escaped char>
<escaped char>                  ::= '\\'  |  '`'  |  '*'  |  '_'  |  '{'
                                 |  '}'   |  '['  |  ']'  |  '('  |  ')'
                                 |  '#'   |  '+'  |  '-'  |  '.'  |  '!'

<header atx>                    ::= '#' <header atx'>
<header atx'>                   ::= '#' <header atx'>
                                 |  ' ' <text with span elements>

<maybe setext header>           ::= '\n' <header setext'>
<header setext'>                ::= '=' <header setext h1>
                                 |  '-' <header setext h2>
                                 |  EPS
                  
<header setext h1>              ::= '=' <spaces or not> <header setext h1'>             ## CAUTION: previous line of MD text was then H1
<header setext h1'>             ::= '=' <spaces or not> <header setext h1'>
                                 |  '\n'

<header setext h2>              ::= '-' <spaces or not> <header setext h2'>             ## CAUTION: previous line of MD text was then H2
<header setext h2'>             ::= '-' <spaces or not> <header setext h2'>
                                 |  '\n'

<horizontal rule>               ::= <hrule star>
                                 |  <hrule hyphen'>

<hrule hyphen>                  ::= '-' <spaces or not> '-' <spaces or not> '-' <hrule hyphen'>
<hrule hyphen'>                 ::= <spaces or not> '-' <hrule hyphen'>
                                 |  '\n'

<hrule star>                    ::= '*' <spaces or not> '*' <spaces or not> '*' <hrule star'>
<hrule star'>                   ::= <spaces or not> '*' <hrule star'>
                                 |  '\n'

<html entity>                   ::= '&' <non space chars> ';'

<html tag>                      ::= <any chars> <html tag'>
<html tag'>                     ::= '/' '>'
                                 |  '>'

<html tag or automatic ilnk>    ::= '<' <html tag or automatic link'>
<html tag or automatic link'>   ::= '/' <any chars> '>'
                                 |  <any chars> <html tag or automatic link">
<html tag or automatic link">   ::= <url'> '>'
                                 |  <html tag'>

<image>                         ::= '!' <link>

<inlined code>                  ::= '`' <any char but `> '`'
                                 |  "``" <inlined code'>
<inlined code'>                 ::= <any chars but `>
                                 |  '`' <inlined code">
<inlined code">                 ::= <any chars but `> <inlined code'>
                                 |  '`'

<inlined link>                  ::= '[' <url> <title> ']'

<item'>                         ::= <text with span elements> <line'>
                                 |  '\n'

<link>                          ::= <inlined link>
                                 |  <referenced link>

<link or reference>             ::= '[' <linked text> ']' <link or reference'>
<link or reference'>            ::= ':' <reference>
                                 |  <link>

<linked text>                   ::= <any chars but ]> 

<list>                          ::= <ordered list>
                                 |  <unordered list>                    ## CAUTION: "paragraphed" list items are not addressed there

<list bullet>                   ::= '*'
                                 | '+'
                                 | '-'

<list'>                         ::= <text with span elements> <list'>
                                 | '\n'

<max 3 spaces>                  ::= ' ' <max 3'>
                                 |  EPS
<max 3'>                        ::= ' ' <max 3''>
                                 |  EPS
<max 3''>                       ::= ' '
                                 |  EPS
                         
<maybe star>                    ::= '*' <maybe star'>
<maybe star'>                   ::= ' '
                                 |  <text with span elements>

<maybe underscore>              ::= '_'
                                 |  <text with span elements>
                         
<line or paragraph end>         ::= '\n' <line or paragraph end'>
<line or paragraph end'>        ::= '\n'                                ## paragraph end
                                 |  EPS                                 ## line end

<ordered item>                  ::= <number> '.' <rest of line> <item'>

<ordered list>                  ::= <ordered item> <ordered list'>
<ordered list'>                 ::= <ordered item> <ordered list'>
                                 | '\n'
                                 |  <ENDOFTEXT>

<paragraph end>                 ::= '\n' '\n'

<reference>                     ::= <spaces or not> <url> <spaces or not> <reference title> '\n'

<referenced link>               ::= <spaces or not> '[' <referenced link'> ']'
<reference linked'>             ::= <any chars but ]>
                                 |  EPS

<reference title>               ::= '\n' <max 3 spaces> <reference title'>                      ## CAUTION: may lead to backward scanning if no '"' in next 4 chars
                                 |  <reference title'>
<reference title'>              ::= '"' <any chars but newline and "> '"' <spaces or not> '\n'  ## (maybe end-of-line is not needed)

<spaces or not>                 ::= ' ' <spaces or not>
                                 |  EPS

<tab>                           ::= '\t'

<title>                         ::= '"' <any chars but newline and "> '"'

<underscore>                    ::= " _ "

<unordered item>                ::= <list bullet> <text with span elements> <item'>

<unordered list>                ::= <unordered item> <unordered list'>
<unordered list'>               ::= <unordered item> <unordered list'>
                                 |  '\n'
                                 |  <ENDOFTEXT>

<url>                           ::= <alphanum chars> <url'>
<url'>                          ::= "://" <any chars but ] and ">
